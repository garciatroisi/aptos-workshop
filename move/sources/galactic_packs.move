
module galactic_workshop::galactic_packs {
    use std::signer;
    use std::string::{Self, String};
    use std::vector;
    use std::bcs;
    use aptos_framework::coin;
    use aptos_framework::object;
    use aptos_token_objects::aptos_token;
    use aptos_token_objects::property_map;

    // Error codes
    const E_NOT_AUTHORIZED: u64 = 1;
    const E_PACK_ALREADY_OPENED: u64 = 2;
    const E_NO_PACKS_AVAILABLE: u64 = 3;
    const E_PACKSTORE_NOT_INITIALIZED: u64 = 4;

    // Constants
    const PACK_PRICE: u64 = 10000000; // 0.1 APT in octas
    const MAX_PACKS: u64 = 100;
    const PACK_SIZE: u64 = 3; // 3 NFTs per pack

    // Collection types
    const COLLECTION_GALACTIC_PACK: u64 = 0;
    const COLLECTION_ALIEN: u64 = 1;    
    const COLLECTION_PREDATOR: u64 = 2;
    const COLLECTION_YODA: u64 = 3;

    // ===== CONSTANTS =====
    // Collection names
    const COLLECTION_NAMES: vector<vector<u8>> = vector[
        b"Galactic Pack",
        b"Alien",
        b"Predator",
        b"Yoda"
    ];
    
    // Collection URIs on IPFS
    const COLLECTION_URIS: vector<vector<u8>> = vector[
        b"ipfs://bafybeicwqydziv5v5mhrplwu7ejfqyf36vugqgbkhju6mbbl6kttiiq33y",
        b"ipfs://bafybeidkl6xkucmemtszbc22yx4l2u27msicv3rmdzevxafb3qh3c6kvwi", 
        b"ipfs://bafybeifk2osoj6qidw6vbecl3t4kg4mqneq5yesywx6bybwjyg35zsyxeu",
        b"ipfs://bafybeigdynpadvw7mra7gn6wph24ewxwtdhknhdpjes3on2sfdxfeyajmy"
    ];

    // ===== STRUCTURES =====
    // PackStore: Global storage for managing pack sales and tracking
    // 
    // This struct stores:
    // - packs: Vector of addresses where packs have been minted
    // - total_sold: Total number of packs sold to date
    struct PackStore has key {
        packs: vector<address>,
        total_sold: u64,
    }
    
    // ManagerInfo: Stores the extend ref, manager address, and payment recipient for creating collections and packst,
    struct ManagerInfo has key {
        extend_ref: object::ExtendRef,
        manager_addr: address,
        payment_recipient: address,
    }
    
    // ===== INITIALIZATION =====
    // Initializes the Galactic Packs module
    // 
    // This function is called once when the module is deployed and:
    // - Creates all NFT collections (Galactic Pack, Alien, Predator, Yoda)
    // - Mints the initial galactic pack for the creator
    // 
    // Parameters:
    // - creator: &signer - The account that deploys this module
    fun init_module(creator: &signer) acquires PackStore {
        // Create an object for managing collections and packs
        let creator_address = signer::address_of(creator);
        let constructor_ref = object::create_sticky_object(creator_address);
        let extend_ref = object::generate_extend_ref(&constructor_ref);

        // Get the manager address from the constructor ref 
        let manager_signer = object::generate_signer_for_extending(&extend_ref);
        let manager_addr = signer::address_of(&manager_signer);
         
        // Create collections for each type using a loop
        create_collections(&manager_signer);
        // Mint galactic pack
        mint_pack(&manager_signer);

        // Get the payment recipient (owner of the object that initializes the contract)
        let payment_recipient = get_payment_recipient();
        
        // Store the extend ref, manager address, and payment recipient for later use
        move_to(creator, ManagerInfo {
            extend_ref,
            manager_addr,
            payment_recipient,
        });
    }

       // ===== FUNCTIONS =====
    
    // Helper function to get the payment recipient address
    // Returns the owner of the object that initializes the contract
    fun get_payment_recipient(): address {
        let contract_object = object::address_to_object<object::ObjectCore>(@galactic_workshop);
        object::owner(contract_object)
    }

    // Function to set a new payment recipient
    // Only the owner of the contract object can call this function
    public entry fun set_payment_recipient(
        caller: &signer,
        new_recipient: address
    ) acquires ManagerInfo {
        // Verify that the caller is the owner of the contract object
        let contract_object = object::address_to_object<object::ObjectCore>(@galactic_workshop);
        let contract_owner = object::owner(contract_object);
        let caller_addr = signer::address_of(caller);
        assert!(contract_owner == caller_addr, E_NOT_AUTHORIZED);
        
        // Update the payment recipient in ManagerInfo
        let manager_info = borrow_global_mut<ManagerInfo>(@galactic_workshop);
        manager_info.payment_recipient = new_recipient;
    }
    
    // Creates all NFT collections by iterating through the collection types
    // 
    // Parameters:
    // - creator: &signer - The account creating all collections
    inline fun create_collections(creator: &signer) {
        let i = 0;
        let total_collections = vector::length(&COLLECTION_NAMES);
        while (i < total_collections) {
            create_collection(creator, i);
            i = i + 1;
        };
    }

    // Creates a single NFT collection based on the collection type
    // 
    // Parameters:
    // - creator: &signer - The account creating the collection
    // - collection_type: u64 - Index of the collection (0=Galactic Pack, 1=Alien, 2=Predator, 3=Yoda)
    inline fun create_collection(creator: &signer, collection_type: u64) {
        // Get collection-specific name and URI from get_collection_info
        let (collection_name, image_uri) = get_collection_info(collection_type);
        
        let description = string::utf8(b"Collection of Galactic Characters");
        
        let max_supply = MAX_PACKS;

        // https://github.com/aptos-labs/aptos-core/blob/main/aptos-move/framework/aptos-token-objects/sources/aptos_token.move
        // Create collection using aptos_token_objects
        aptos_token::create_collection(
            creator,
            description,
            max_supply,
            collection_name,
            image_uri,
            true, true, true, true, true, true, true, false, false,
            5, 100 // 5% royalty
        );
    }
 
    // Retrieves collection information by collection type index
    // 
    // Parameters:
    // - collection_type: u64 - Index of the collection (0=Galactic Pack, 1=Alien, 2=Predator, 3=Yoda)
    // 
    // Returns: (String, String) - Tuple containing (collection_name, image_uri)
    // 
    // Aborts if:
    // - collection_type is out of bounds of COLLECTION_NAMES vector
    // 
    // Example:
    // - collection_type = 1 returns ("Alien", "ipfs://...")
    public fun get_collection_info(collection_type: u64): (String, String) {
        (string::utf8(*vector::borrow(&COLLECTION_NAMES, collection_type)), 
         string::utf8(*vector::borrow(&COLLECTION_URIS, collection_type)))
    }

    // Mints the initial collection of MAX_PACKS (100) galactic packs for the creator
    // 
    // Parameters:
    // - creator: &signer - The account that will own all the packs
    // 
    // This function:
    // - Creates PackStore if it doesn't exist
    // - Mints MAX_PACKS number of packs with unique pack numbers
    // - Each pack has properties: "Pack Number" and "Opened" (initially false)
    // - Stores all pack token addresses in PackStore.packs vector
    inline fun mint_pack(creator: &signer) {
        let creator_addr = signer::address_of(creator);    

        move_to(creator, PackStore {
            packs: vector::empty(),
            total_sold: 0,
        });

        let pack_store = borrow_global_mut<PackStore>(creator_addr);
        
        let (collection_name, image_uri) = get_collection_info(COLLECTION_GALACTIC_PACK);
        let i = 1;
        while (i <= MAX_PACKS) {
            let property_keys = vector::empty<string::String>();
            let property_types = vector::empty<string::String>();
            let property_values = vector::empty<vector<u8>>();

            // Add pack number as property
            vector::push_back(&mut property_keys, string::utf8(b"Pack Number"));
            vector::push_back(&mut property_types, string::utf8(b"u64"));
            vector::push_back(&mut property_values, bcs::to_bytes(&i));

            // Add opened property as false initially
            vector::push_back(&mut property_keys, string::utf8(b"Opened"));
            vector::push_back(&mut property_types, string::utf8(b"bool"));
            vector::push_back(&mut property_values, bcs::to_bytes(&false));

            let token_id = aptos_token::mint_token_object(
                creator, //creator
                collection_name, //collection name
                string::utf8(b"A mysterious pack containing galactic characters"), //description
                collection_name, //name
                image_uri, //metadata uri
                property_keys, //property keys
                property_types, //property types    
                property_values, //property values
            );
            
            //Get the token id as an address
            let token_id_address = object::object_address(&token_id);
            // Store the pack
            vector::push_back(&mut pack_store.packs, token_id_address);
            
            i = i + 1;
        };
    }

    
    #[view]
    public fun get_total_sold(): u64 acquires PackStore, ManagerInfo {
        //obtener el extend ref
        let manager_info = borrow_global<ManagerInfo>(@galactic_workshop);
        let manager_addr = manager_info.manager_addr;
        borrow_global<PackStore>(manager_addr).total_sold
    }

    #[view]
    public fun get_pack_token_id(index: u64): address acquires PackStore, ManagerInfo {
        let manager_info = borrow_global<ManagerInfo>(@galactic_workshop);
        let manager_addr = manager_info.manager_addr;
        let pack_store = borrow_global<PackStore>(manager_addr);
        *vector::borrow(&pack_store.packs, index)
    }

    // Allows a user to purchase a galactic pack from the creator
    // 
    // Parameters:
    // - user: &signer - The account buying the pack
    // 
    // Returns: None
    // 
    // This function:
    // - Checks if packs are available for purchase
    // - Transfers PACK_PRICE (0.1 APT) from user to creator
    // - Transfers the pack NFT from creator to user using manager_object
    // - Increments total_sold counter
    // 
    // Aborts if:
    // - Creator doesn't have a PackStore
    // - No packs available (total_sold >= MAX_PACKS)
    // - User doesn't have enough APT
    // - Transfer fails
    public entry fun buy_pack(
        user: &signer, 
    ) acquires PackStore, ManagerInfo {
        let user_addr = signer::address_of(user);
        
        let manager_info = borrow_global<ManagerInfo>(@galactic_workshop);
        let manager_addr = manager_info.manager_addr;
        let payment_recipient = manager_info.payment_recipient;
        let pack_store = borrow_global_mut<PackStore>(manager_addr);
        assert!(pack_store.total_sold < MAX_PACKS, E_NO_PACKS_AVAILABLE); 
    
        // Transfer payment from user to payment recipient (creator)
        // https://github.com/aptos-labs/aptos-core/blob/main/aptos-move/framework/aptos-token-objects/doc/property_map.md#0x4_property_map
        let user_coin = coin::withdraw<aptos_framework::aptos_coin::AptosCoin>(user, PACK_PRICE);
        coin::deposit(payment_recipient, user_coin);

        // Get the pack using total_sold as index
        let pack_token_id = vector::borrow(&pack_store.packs, pack_store.total_sold); 
        let pack_object = object::address_to_object<object::ObjectCore>(*pack_token_id);

        // Increment total sold
        pack_store.total_sold = pack_store.total_sold + 1;

        // generate signer from extend ref
        let manager_signer = object::generate_signer_for_extending(&manager_info.extend_ref);
        // Transfer the pack object from the creator to the user using manager_signer
        object::transfer(&manager_signer, pack_object, user_addr);
    }

    // Allows a user to open a galactic pack and receive random NFTs
    // 
    // Parameters:
    // - user: &signer - The account opening the pack (must own the pack)
    // - pack_token_id: address - The address of the pack NFT to open
    // 
    // Returns: None
    // 
    // This function:
    // - Verifies the user owns the pack
    // - Checks if the pack is already opened
    // - Marks the pack as opened using manager_object
    // - Mints PACK_SIZE (3) random NFTs and transfers them to the user
    // 
    // Aborts if:
    // - User doesn't own the pack
    // - Pack is already opened
    // - Property update fails
    // - NFT minting fails
    // 
    // Requires #[randomness] attribute for NFT rarity generation
    #[randomness]
    entry fun open_pack(
        user: &signer,
        pack_token_id: address
    ) acquires ManagerInfo {
        open_pack_internal(user, pack_token_id);
    }

    // Internal function that handles the pack opening logic
    // 
    // Parameters:
    // - user: &signer - The account opening the pack
    // - pack_token_id: address - The address of the pack NFT to open
    // 
    // Returns: None
    // 
    // This function:
    // - Validates pack ownership
    // - Checks if pack is already opened
    // - Updates pack "Opened" property to true using manager_object
    // - Triggers NFT minting and transfer
    // 
    // Aborts if:
    // - User doesn't own the pack
    // - Pack is already opened
    // - Property update fails
    fun open_pack_internal(
        user: &signer,
        pack_token_id: address
    ) acquires ManagerInfo {
        let user_addr = signer::address_of(user);
    
        let pack_object = object::address_to_object<object::ObjectCore>(pack_token_id);
        // Check if the user has the pack
        // https://github.com/aptos-labs/aptos-core/blob/main/aptos-move/framework/aptos-framework/doc/object.md#0x1_object_is_owner
        assert!(object::is_owner(pack_object, user_addr), E_NOT_AUTHORIZED); 

        // Check if the pack is already opened
        let opened_property = property_map::read_bool(&pack_object, &string::utf8(b"Opened"));
        assert!(!opened_property, E_PACK_ALREADY_OPENED);
        
        // Get the extend ref and generate signer
        let manager_info = borrow_global<ManagerInfo>(@galactic_workshop);
        let manager_signer = object::generate_signer_for_extending(&manager_info.extend_ref);
        
        // Update opened property to true using manager_signer
        // https://github.com/aptos-labs/aptos-core/blob/main/aptos-move/framework/aptos-token-objects/doc/aptos_token.md#0x4_aptos_token_update_property
        aptos_token::update_property(
            &manager_signer,
            pack_object,
            string::utf8(b"Opened"),
            string::utf8(b"bool"),
            bcs::to_bytes(&true),
        );
        
        // Mint PACK_SIZE tokens of different types and transfer to user
        mint_and_transfer_tokens(&manager_signer, user_addr);
    }
    
    // Mints PACK_SIZE (3) random NFTs and transfers them to the user
    // 
    // Parameters:
    // - manager_signer: &signer - The manager signer for minting the NFTs
    // - user_addr: address - The address receiving the NFTs
    // 
    // This function:
    // - Mints one NFT from each collection (Alien, Predator, Yoda)
    // - Each NFT gets a random rarity level
    // - Transfers all NFTs directly to the user
    // 
    // Aborts if:
    // - NFT minting fails
    // - Transfer fails
    // - Randomness service unavailable
    inline fun mint_and_transfer_tokens(manager_signer: &signer, user_addr: address) {
        let i = 0;
        while (i < PACK_SIZE) {
            // Set collection type based on iteration, starting from 1 (0 is Galactic Pack)
            let collection_type = i + 1;
            
            // Get collection info
            let (collection_name, collection_uri) = get_collection_info(collection_type);
            
            // Get random rarity for this token
            let rarity = get_random_rarity();

            // Create properties for the token
            let property_keys = vector::empty<string::String>();
            let property_types = vector::empty<string::String>();
            let property_values = vector::empty<vector<u8>>();            
       
            // Add rarity property
            vector::push_back(&mut property_keys, string::utf8(b"Rarity"));
            vector::push_back(&mut property_types, string::utf8(b"0x1::string::String"));
            vector::push_back(&mut property_values, bcs::to_bytes(&rarity));                         
            
            // Mint the token
            let token_id = aptos_token::mint_token_object(
                manager_signer,
                collection_name,
                string::utf8(b"An epic warrior from the depths of space"),
                collection_name,
                collection_uri,
                property_keys,
                property_types,
                property_values,
            );
            
            // Transfer the token to the user
            object::transfer(manager_signer, token_id, user_addr);
            
            i = i + 1;
        };
    }

    // Generates a random rarity level for NFT packs using Aptos randomness
    // 
    // Returns: string::String - The rarity level as a string
    // 
    // Rarity distribution:
    // - Common: 30% chance
    // - Uncommon: 25% chance  
    // - Rare: 20% chance
    // - Epic: 12% chance
    // - Legendary: 8% chance
    // - Mythic: 5% chance
    // 
    // https://aptos.dev/build/smart-contracts/randomness
    fun get_random_rarity(): string::String {
        // Balanced rarity system with predefined probabilities:
        // NOTE: the randomness API does not currently prevent undergassing attacks.
        let random_value = aptos_framework::randomness::u64_range(0, 100);

        // Default rarity is "Mythic" (lowest probability case).
        let rarity = string::utf8(b"Mythic");

        // Assign rarity based on the generated random value.
        // All conditions are checked to ensure constant gas usage.
        if (random_value < 30) rarity = string::utf8(b"Common");                         // 0–29 → Common
        if (random_value >= 30 && random_value < 55) rarity = string::utf8(b"Uncommon"); // 30–54 → Uncommon
        if (random_value >= 55 && random_value < 75) rarity = string::utf8(b"Rare");     // 55–74 → Rare
        if (random_value >= 75 && random_value < 87) rarity = string::utf8(b"Epic");     // 75–86 → Epic
        if (random_value >= 87 && random_value < 95) rarity = string::utf8(b"Legendary");// 87–94 → Legendary
                                                                                         // 95–99 → Mythic (default) 
        rarity

        //Incorrect:
        // if (random_value < 30) return string::utf8(b"Common");
        // if (random_value < 55) return string::utf8(b"Uncommon");
        // if (random_value < 75) return string::utf8(b"Rare");
        // if (random_value < 87) return string::utf8(b"Epic");
        // if (random_value < 95) return string::utf8(b"Legendary");
        // string::utf8(b"Mythic")
        // Problem:
        // - Each branch returns early, so the number of executed comparisons depends on `random_value`.
        // - This makes the gas cost variable and can be exploited in undergassing attacks.
        // - Always ensure every path executes the same amount of work before returning.
    }  

    // ===== TEST FUNCTIONS =====

    // Test-only function for opening packs (easier to use in tests)
    #[test_only]
    #[lint::allow_unsafe_randomness]
    public fun test_open_pack(
        user: &signer,
        pack_token_id: address
    ) acquires ManagerInfo {
        open_pack_internal(user, pack_token_id);
    }

    // Public function for testing initialization
    #[test_only]
    public fun initialize_for_testing(creator: &signer) acquires PackStore {
        // Create an object for managing collections and packs
        let creator_address = signer::address_of(creator);
        let constructor_ref = object::create_sticky_object(creator_address);
        let extend_ref = object::generate_extend_ref(&constructor_ref);
        
        // Get the manager address from the constructor ref
        let manager_signer = object::generate_signer_for_extending(&extend_ref);
        let manager_addr = signer::address_of(&manager_signer);
        
        // Store the extend ref, manager address, and payment recipient for later use
        move_to(creator, ManagerInfo {
            extend_ref,
            manager_addr,
            payment_recipient: creator_address,
        });
        
        // Create collections for each type using a loop
        create_collections(&manager_signer);
        // Mint galactic pack
        mint_pack(&manager_signer);
    }

}
